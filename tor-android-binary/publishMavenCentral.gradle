// Publish to Maven Central
nexusPublishing {
  repositories {
      sonatype {
          username = System.getenv("OSS_USERNAME")
          password = System.getenv("OSS_PASSWORD")
          stagingProfileId = System.getenv("OSS_STAGING_PROFILE_ID")
      }
  }
}

// If you want to publish your sources as well
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId LIBRARY_GROUP
                artifactId LIBRARY_ARTIFACT_ID
                version LIBRARY_VERSION_NAME

                from components.release
                artifact androidSourcesJar

                pom {
                    name = LIBRARY_ARTIFACT_ID
                    description = 'Tor Android'
                    url = LIBRARY_URL
                    licenses {
                        // Your licensing information
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'instrap'
                            name = 'Madrapps'
                            email = 'madrasappfactory@gmail.com'
                        }
                        // More developers if any...
                    }

                    scm {
                        connection = LIBRARY_GIT_URL
                        developerConnection = LIBRARY_GIT_URL
                        url = LIBRARY_URL
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            System.getenv("OSS_SIGNING_KEY_ID"),
            System.getenv("OSS_SIGNING_KEY"),
            System.getenv("OSS_SIGNING_PASSWORD"),
    )
    sign publishing.publications
}
